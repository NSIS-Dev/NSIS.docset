<html>
<header>
	<link rel="stylesheet" href="../../../css/bootstrap.min.css">
	<link rel="stylesheet" href="../../../css/highlight.css">
</header>
<body>

<div class="container">

<h1>GetTime</h1>

<hr>

<ul>
<li>Get local or system time.</li>
<li>Get file time (access, creation and modification).</li>
</ul>

<h2>Syntax:</h2>

<pre><code>${GetTime} "[File]" "[Option]" $var1 $var2 $var3 $var4 $var5 $var6 $var7
"[File]"        ; Ignored if "L" or "LS"
                ;
"[Option]"      ; [Options]
                ;   L   Local time
                ;   A   last Access file time
                ;   C   Creation file time
                ;   M   Modification file time
                ;   LS  System time (UTC)
                ;   AS  last Access file time (UTC)
                ;   CS  Creation file time (UTC)
                ;   MS  Modification file time (UTC)
                ;
$var1           ; Result1: day
$var2           ; Result2: month
$var3           ; Result3: year
$var4           ; Result4: day of week name
$var5           ; Result5: hour
$var6           ; Result6: minute
$var7           ; Result7: seconds
</code></pre>

<p>Note:</p>

<ul>
<li>Error flag if file isn't exist </li>
<li>Error flag if syntax error </li>
<li>See also <a href="http://nsis.sourceforge.net/Time_plugin">Time plugin</a></li>
</ul>

<h2>Examples:</h2>

<h3>Get local time</h3>

<pre><code>Section
    ${GetTime} "" "L" $0 $1 $2 $3 $4 $5 $6
    ; $0="01"      day
    ; $1="04"      month
    ; $2="2005"    year
    ; $3="Friday"  day of week name
    ; $4="16"      hour
    ; $5="05"      minute
    ; $6="50"      seconds

    MessageBox MB_OK 'Date=$0/$1/$2 ($3)$\nTime=$4:$5:$6'
SectionEnd
</code></pre>

<h3>Get file time</h3>

<pre><code>Section
    ${GetTime} "$WINDIR\Explorer.exe" "C" $0 $1 $2 $3 $4 $5 $6
    ; $0="12"       day
    ; $1="10"       month
    ; $2="2004"     year
    ; $3="Tuesday"  day of week name
    ; $4="2"        hour
    ; $5="32"       minute
    ; $6="03"       seconds

    IfErrors 0 +2
    MessageBox MB_OK "Error" IDOK +2
    MessageBox MB_OK 'Date=$0/$1/$2 ($3)$\nTime=$4:$5:$6'
SectionEnd
</code></pre>

<h3>Get system time</h3>

<pre><code>Section
    ${GetTime} "" "LS" $0 $1 $2 $3 $4 $5 $6
    ; $0="01"      day
    ; $1="04"      month
    ; $2="2005"    year
    ; $3="Friday"  day of week name
    ; $4="11"      hour
    ; $5="05"      minute
    ; $6="50"      seconds

    MessageBox MB_OK 'Date=$0/$1/$2 ($3)$\nTime=$4:$5:$6'
SectionEnd
</code></pre>

<h3>Convert time to 12-hour format AM/PM</h3>

<pre><code>Section
    ${GetTime} "" "L" $0 $1 $2 $3 $4 $5 $6

    StrCmp $4 0 0 +3
    StrCpy $4 12
    goto +3
    StrCmp $4 12 +5
    IntCmp $4 12 0 0 +3
    StrCpy $7 AM
    goto +3
    IntOp $4 $4 - 12
    StrCpy $7 PM

    MessageBox MB_OK 'Date=$0/$1/$2 ($3)$\nTime=$4:$5:$6 $7'
SectionEnd
</code></pre>

<h2>Credits:</h2>

<p>Written by <a href="http://nsis.sourceforge.net/User:Instructor">Instructor</a></p>

<hr>

</div>

<script src="../../../js/highlight.pack.js"></script>
<script>hljs.initHighlightingOnLoad();</script>

</body>
</html>
