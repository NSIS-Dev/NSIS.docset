<html>
<header>
	<link rel="stylesheet" href="../../../css/bootstrap.min.css">
	<link rel="stylesheet" href="../../../css/highlight.css">
</header>
<body>

<div class="container">

<h1>FileReadFromEnd</h1>

<hr>

<p>Read text file from end line by line.</p>

<h2>Syntax:</h2>

<pre><code>${FileReadFromEnd} "[File]" "Function"

"[File]"      ; Input text file
"Function"    ; Callback function

Function "Function"
    ; $9       current line
    ; $8       current line number
    ; $7       current line negative number

    ; $R0-$R9  are not used (save data in them).
    ; ...

    Push $var      ; If $var="StopFileReadFromEnd"  Then exit from function
FunctionEnd
</code></pre>

<p>Note:</p>

<ul>
<li>Error flag if input file doesn't exist</li>
</ul>

<h2>Examples:</h2>

<h3>Read and display lines</h3>

<pre><code>Section
    ${FileReadFromEnd} "C:\a.log" "Example1"

    IfErrors 0 +2
    MessageBox MB_OK "Error"
SectionEnd

Function Example1
    MessageBox MB_OKCANCEL '"Line"=[$9]$\n   "#"=[$8]$\n  "-#"=[$7]' IDOK +2
    StrCpy $0 StopFileReadFromEnd

    Push $0
FunctionEnd
</code></pre>

<h3>Reverse text file</h3>

<pre><code>Section
    GetTempFileName $R0
    FileOpen $R1 $R0 w
    ${FileReadFromEnd} "C:\a.log" "Example2"
    FileClose $R1

    IfErrors 0 +2
    MessageBox MB_OK "Error" IDOK +2
    Exec '"notepad.exe" "$R0"'
SectionEnd

Function Example2
    StrCmp $7 -1 0 +5
    StrCpy $1 $9 1 -1
    StrCmp $1 '$\n' +3
    StrCmp $1 '$\r' +2
    StrCpy $9 '$9$\r$\n'

    FileWrite $R1 "$9"

    Push $0
FunctionEnd
</code></pre>

<h2>Credits:</h2>

<p>Written by <a href="http://nsis.sourceforge.net/User:Instructor">Instructor</a></p>

<hr>

</div>

<script src="../../../js/highlight.pack.js"></script>
<script>hljs.initHighlightingOnLoad();</script>

</body>
</html>
